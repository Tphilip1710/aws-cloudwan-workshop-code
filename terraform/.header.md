# AWS Cloud WAN - Terraform

AWS Cloud WAN is a managed wide-area networking (WAN) service that you can use to build, manage, and monitor an unified global network that connects resources running across your cloud and on-premises environments. It provides a central dashboard from which you can connect on-premises branch offices, data centers, and Amazon Virtual Private Clouds (VPCs) across the AWS global network. You can use simple network policies to centrally configure and automate network management and security tasks, and get a complete view of your global network.

This repository shows you an example in Terraform of global communication with AWS Cloud WAN, showing the same architecture you can build in the [AWS Cloud WAN Workshop](https://catalog.workshops.aws/cloudwan/en-US).

![architecture diagram](../images/cloud_wan_architecture.png "AWS Cloud WAN diagram")

**NOTE**: The resources created incur costs in your AWS Account. Consider deleting the resources created once your tests are done. For more information, check the [AWS Cloud WAN pricing](https://aws.amazon.com/cloud-wan/pricing/).

## Pre Requisites

* An AWS account with an IAM user that has appropriate permissions
* Terraform installed

## Deployment instructions

* Clone the repository
* Modify *variables.tf* and *locals.tf* in the project root directory if there are any changes needed to the VPCs/Subnets/Security Groups/VPC Endpoints related configuration. The defaults would create required VPCs and other resources in **us-east-1** and **eu-west-1** AWS Regions.
* The initial Core Network Policy is specified in `cloudwan_policy.tf`. This would be the default initial policy with attachments that would be used when the Core Network is created.
* Initialize Terraform using `terraform init`.
* Now you can deploy the rest of the infrastructure using `terraform apply`.
* Once the deployment is finished, you can check in the [AWS Cloud WAN management console](https://us-west-2.console.aws.amazon.com/networkmanager/home) that the Spoke VPC production attachments require acceptance. In the `main.tf` file you will find commented the piece of code needed to accept those attachments.
* Please make a note of the outputs giving you the Cloud WAN VPC attachments for the Inspection VPCs - `cloud_wan.attachments.{aws_region}.inspection` - as this will be needed for next run.

## Updating Core Network Policy for Routing

**NOTE**: this configuration step is an MPV design, we are working on removing the need of this update and have everything working in the same `terraform apply`.

* After the entire infrastructure stack gets created, to facilitate Routing and to enable communication between attachments, the Routing configuration has to be updated.
* This would be a manual step to update the `core_nw_policy` in `cloudwan_policy.tf` and will soon be automated and baked into the original apply process.
* Based on the Outputs received in prior run, please update the policy document below and do `terraform apply` again.

```hcl
data "aws_networkmanager_core_network_policy_document" "core_nw_policy" {
  core_network_configuration {
    vpn_ecmp_support = false
    asn_ranges       = ["64512-64555"]
    edge_locations {
      location = "ap-southeast-1"
      asn      = 64512
    }
    edge_locations {
      location = "ap-southeast-2"
      asn      = 64513
    }
  }
  segments {
    name                          = "shared"
    description                   = "Segment for shared services"
    require_attachment_acceptance = false
  }

  segments {
    name                          = "prod"
    description                   = "Segment for prod services"
    require_attachment_acceptance = true
  }

  segments {
    name                          = "nonprod"
    description                   = "Segment for nonprod services"
    require_attachment_acceptance = false
  }

  segment_actions {
    action  = "create-route"
    segment = "prod"
    destination_cidr_blocks = [
      "0.0.0.0/0"
    ]
    destinations = [
      ***INSERT_OUTPUTS_FROM_PREVIOUS_APPLY***
    ]
  }
  segment_actions {
    action  = "create-route"
    segment = "nonprod"
    destination_cidr_blocks = [
      "0.0.0.0/0"
    ]
    destinations = [
      ***INSERT_OUTPUTS_FROM_PREVIOUS_APPLY***
    ]
  }
  segment_actions {
    action     = "share"
    mode       = "attachment-route"
    segment    = "shared"
    share_with = ["*"]
  }

  attachment_policies {
    rule_number     = 100
    condition_logic = "or"
    conditions {
      type = "tag-exists"
      key  = "prod"
    }
    action {
      association_method = "constant"
      segment            = "prod"
    }
  }

  attachment_policies {
    rule_number     = 200
    condition_logic = "or"
    conditions {
      type = "tag-exists"
      key  = "nonprod"
    }
    action {
      association_method = "constant"
      segment            = "nonprod"
    }
  }

  attachment_policies {
    rule_number     = 300
    condition_logic = "or"
    conditions {
      type = "tag-exists"
      key  = "shared"
    }
    action {
      association_method = "constant"
      segment            = "shared"
    }
  }
}
```

## References

* [AWS Cloud WAN documentation](https://docs.aws.amazon.com/vpc/latest/cloudwan/what-is-cloudwan.html).
* [AWS Cloud WAN Workshop](https://catalog.workshops.aws/cloudwan/en-US).
* Blog post: [Introducing AWS Cloud WAN (Preview)](https://aws.amazon.com/blogs/networking-and-content-delivery/introducing-aws-cloud-wan-preview/).

## Security

See [CONTRIBUTING](CONTRIBUTING.md#security-issue-notifications) for more information.

## License

This library is licensed under the MIT-0 License. See the LICENSE file.
