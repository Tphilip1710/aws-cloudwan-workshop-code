# AWS Cloud WAN - Terraform

AWS Cloud WAN is a managed wide-area networking (WAN) service that you can use to build, manage, and monitor an unified global network that connects resources running across your cloud and on-premises environments. It provides a central dashboard from which you can connect on-premises branch offices, data centers, and Amazon Virtual Private Clouds (VPCs) across the AWS global network. You can use simple network policies to centrally configure and automate network management and security tasks, and get a complete view of your global network.

This repository shows you an example in Terraform of global communication with AWS Cloud WAN, showing the same architecture you can build in the [AWS Cloud WAN Workshop](https://catalog.workshops.aws/cloudwan/en-US).

![architecture diagram](../images/cloud_wan_architecture.png "AWS Cloud WAN diagram")

**NOTE**: The resources created incur costs in your AWS Account. Consider deleting the resources created once your tests are done. For more information, check the [AWS Cloud WAN pricing](https://aws.amazon.com/cloud-wan/pricing/).

## Pre Requisites

* An AWS account with an IAM user that has appropriate permissions
* Terraform installed

## Deployment instructions

* Clone the repository
* Modify *variables.tf*, *singapore_locals.tf* and *sydney_locals.tf* in the project root directory if there are any changes needed to the VPCs/Subnets/Security Groups/VPC Endpoints related configuration. The defaults would create required VPCs and other resources in **ap-southeast-1** and Sydney **ap-southeast-2** regions.
* The initial Network Policy for Core Network is specified in `data.tf`. This would be the default initial policy with attachments that would be used when the Core Network is created.
* Initialize Terraform using `terraform init`.
* Now you can deploy the rest of the infrastructure using `terraform apply`.
* Please make a note of outputs `core_nw_attachments_egress_singapore` and `core_nw_attachments_egress_sydney` as this will be needed for next run.

**Warning:** If you get any error when creating the VPC routes: *Error: error creating Route in Route Table (rtb-XXXX) with destination (0.0.0.0/0): InvalidCoreNetworkArn.NotFound: The core network arn 'arn:aws:networkmanager::XXXXXX:core-network/core-network-XXXXX' does not exist.*. Don't worry, you may need to accept the VPC attachment from the Cloud WAN console. Once this attachment is accepted and created, do `terraform apply` again and the VPC routes will be created.

## Updating Core Network Policy for Routing

**NOTE**: this configuration step is an MPV design, we are working on removing the need of this update and have everything working in the same `terraform apply`.

* After the entire infrastructure stack gets created, to facilitate Routing and to enable communication between attachments, the Routing configuration has to be updated.
* This would be a manual step to update the `core_nw_policy` in `data.tf` and will soon be automated and baked into the original apply process.
* Based on the Outputs received in prior run, please update the policy document below and do `terraform apply` again.

```hcl
data "aws_networkmanager_core_network_policy_document" "core_nw_policy" {
  core_network_configuration {
    vpn_ecmp_support = false
    asn_ranges       = ["64512-64555"]
    edge_locations {
      location = "ap-southeast-1"
      asn      = 64512
    }
    edge_locations {
      location = "ap-southeast-2"
      asn      = 64513
    }
  }
  segments {
    name                          = "shared"
    description                   = "Segment for shared services"
    require_attachment_acceptance = false
  }

  segments {
    name                          = "prod"
    description                   = "Segment for prod services"
    require_attachment_acceptance = false
  }

  segments {
    name                          = "nonprod"
    description                   = "Segment for nonprod services"
    require_attachment_acceptance = false
  }

  segment_actions {
    action  = "create-route"
    segment = "prod"
    destination_cidr_blocks = [
      "0.0.0.0/0"
    ]
    destinations = [
      ***INSERT_OUTPUTS_FROM_PREVIOUS_APPLY***
    ]
  }
  segment_actions {
    action  = "create-route"
    segment = "nonprod"
    destination_cidr_blocks = [
      "0.0.0.0/0"
    ]
    destinations = [
      ***INSERT_OUTPUTS_FROM_PREVIOUS_APPLY***
    ]
  }
  segment_actions {
    action     = "share"
    mode       = "attachment-route"
    segment    = "shared"
    share_with = ["*"]
  }

  attachment_policies {
    rule_number     = 100
    condition_logic = "or"
    conditions {
      type = "tag-exists"
      key  = "prod"
    }
    action {
      association_method = "constant"
      segment            = "prod"
    }
  }

  attachment_policies {
    rule_number     = 200
    condition_logic = "or"
    conditions {
      type = "tag-exists"
      key  = "nonprod"
    }
    action {
      association_method = "constant"
      segment            = "nonprod"
    }
  }

  attachment_policies {
    rule_number     = 300
    condition_logic = "or"
    conditions {
      type = "tag-exists"
      key  = "shared"
    }
    action {
      association_method = "constant"
      segment            = "shared"
    }
  }
}
```

## References

* [AWS Cloud WAN documentation](https://docs.aws.amazon.com/vpc/latest/cloudwan/what-is-cloudwan.html).
* [AWS Cloud WAN Workshop](https://catalog.workshops.aws/cloudwan/en-US).
* Blog post: [Introducing AWS Cloud WAN (Preview)](https://aws.amazon.com/blogs/networking-and-content-delivery/introducing-aws-cloud-wan-preview/).

## Security

See [CONTRIBUTING](CONTRIBUTING.md#security-issue-notifications) for more information.

## License

This library is licensed under the MIT-0 License. See the LICENSE file.